buildscript {
	ext {
		kotlinVersion = '1.0.4'
		anxionRelease = '1.4.0'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
		classpath "pl.allegro.tech.build:axion-release-plugin:${anxionRelease}"
	}
}

plugins {
	id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply from: 'gradle/integration-test.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

gitProperties {
	gitRepositoryRoot = project.rootProject.file('./..')
}

/** Versioning */
scmVersion {
	repository {
		directory = project.rootProject.file('./..')
	}
	localOnly = true // never connect to remote
	tag {
		prefix = ''
	}
	versionIncrementer 'incrementMinor'
}

project.version = scmVersion.version

jar {
	baseName = rootProject.name
	version = project.version
}

war {
	baseName = 'bgs'
	version = project.version
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-jdbc'
	compile "org.springframework.boot:spring-boot-starter-actuator"
//	compile 'org.springframework.boot:spring-boot-devtools'
	compile 'org.springframework.boot:spring-boot-starter-aop'
	compile('org.springframework.boot:spring-boot-starter-security')
	compile 'io.springfox:springfox-swagger2:2.6.0'
	compile 'io.springfox:springfox-swagger-ui:2.6.0'
	compile 'com.google.guava:guava:19.0'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'com.javaslang:javaslang:1.2.3'
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.5"
	compile 'com.zaxxer:HikariCP:2.5.1'
	compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
	compile group: 'org.json', name: 'json', version: '20160810'
	compile files('libs/ojdbc7.jar')

	compile "io.dropwizard.metrics:metrics-core"
	compile "io.dropwizard.metrics:metrics-graphite:${dropwizard_metrics_version}"
	compile "io.dropwizard.metrics:metrics-healthchecks:${dropwizard_metrics_version}"
	compile "io.dropwizard.metrics:metrics-jvm:${dropwizard_metrics_version}"
	compile "io.dropwizard.metrics:metrics-servlet:${dropwizard_metrics_version}"
	compile "io.dropwizard.metrics:metrics-json:${dropwizard_metrics_version}"
	compile ("io.dropwizard.metrics:metrics-servlets:${dropwizard_metrics_version}") {
		exclude(module: 'metrics-healthchecks')
	}
	compile ("com.ryantenney.metrics:metrics-spring:${metrics_spring_version}") {
		exclude(module: 'metrics-core')
		exclude(module: 'metrics-healthchecks')
	}
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

	testCompile 'org.springframework.boot:spring-boot-starter-test'

//	compile('org.springframework.boot:spring-boot-starter-validation')
//	optional "org.springframework.boot:spring-boot-configuration-processor"
}

compileJava.dependsOn(processResources)

springBoot  {
	buildInfo()
	executable = true
}

bootRun {
	addResources = true
}

// generate gradle wrapper with specific version
task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}